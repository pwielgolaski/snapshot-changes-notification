buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.3.3'
        classpath 'com.github.rodm:gradle-teamcity-plugin:0.8.1'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.jfrog.bintray'

scmVersion {
    tag {
        prefix = project.name
    }
    versionCreator 'versionWithBranch'
    localOnly = true
}

version = scmVersion.version

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {

}

ext {
    teamcityVersion = '9.1.5'
    teamcityDir = "$rootDir/servers/TeamCity-${teamcityVersion}"
    teamcityDataDir = "$rootDir/data/" + (teamcityVersion =~ /(\d+\.\d+).*/)[0][1]
    teamcityJavaHome = System.properties['java.home']
}

teamcity {
    version = teamcityVersion

    descriptor {
        name = project.name
        displayName = project.name
        version = project.version
        description = 'Snapshot changes in notifications'
        downloadUrl = 'https://github.com/pwielgolaski/snapshot-changes-notification/'
    }

    homeDir = file(teamcityDir)
    dataDir = file(teamcityDataDir)
    javaHome = file(teamcityJavaHome)
}

deployPlugin.dependsOn assemble

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    filesSpec {
        from 'build/distributions'
        into '.'
    }
    pkg {
        repo = 'generic'
        name = project.name
        desc = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/pwielgolaski/snapshot-changes-notification.git'
        labels = ['teamcity', 'snapshot']
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'Snapshot changes in notifications'
        }
    }
}

bintrayUpload.onlyIf {
    !(version ==~ /.*SNAPSHOT/)
}